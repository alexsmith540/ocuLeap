<html>
<head>
<title>ocuLeap</title>
<link rel="stylesheet" type="text/css" href="css/style.css" />
<script src="bower_components/jquery/jquery.min.js"></script>
<script src="bower_components/leapjs/leap.min.js"></script>
<script src="bower_components/three.js/build/three.min.js"></script>

<script src="bower_components/vr.js/lib/vr.js"></script>
<script src="external/riftControls/oculusControls.js"></script>
<script src="external/riftControls/oculusEffect.js"></script>
<!--a couple of shaders for the pointcloud so we can talk to each individual point-->
<script type="x-shader/x-vertex" id="vertexshader">
attribute float size;
attribute vec3 ca;
varying vec3 vColor;
void main() {
    vColor = ca;
    vec4 mvPosition = modelViewMatrix * vec4( position, 1.0 );
    gl_PointSize = size * ( 150.0 / length( mvPosition.xyz ) );
    gl_Position = projectionMatrix * mvPosition;
}
</script>
<script type="x-shader/x-fragment" id="fragmentshader">
uniform vec3 color;
varying vec3 vColor;
void main() {
    gl_FragColor = /*outColor * */vec4( color * vColor, 1.0 );
}
</script>
<!--end shaders -->
<!--ok lets get a little fancy pants with some shaders -->
<script type="x-shader/x-vertex" id="perlin_vertexShader">
        //
// GLSL textureless classic 3D noise "cnoise",
// with an RSL-style periodic variant "pnoise".
// Author:  Stefan Gustavson (stefan.gustavson@liu.se)
// Version: 2011-10-11
//
// Many thanks to Ian McEwan of Ashima Arts for the
// ideas for permutation and gradient selection.
//
// Copyright (c) 2011 Stefan Gustavson. All rights reserved.
// Distributed under the MIT license. See LICENSE file.
// https://github.com/ashima/webgl-noise
//

vec3 mod289(vec3 x)
{
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec4 mod289(vec4 x)
{
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

vec4 permute(vec4 x)
{
  return mod289(((x*34.0)+1.0)*x);
}

vec4 taylorInvSqrt(vec4 r)
{
  return 1.79284291400159 - 0.85373472095314 * r;
}

vec3 fade(vec3 t) {
  return t*t*t*(t*(t*6.0-15.0)+10.0);
}

// Classic Perlin noise
float cnoise(vec3 P)
{
  vec3 Pi0 = floor(P); // Integer part for indexing
  vec3 Pi1 = Pi0 + vec3(1.0); // Integer part + 1
  Pi0 = mod289(Pi0);
  Pi1 = mod289(Pi1);
  vec3 Pf0 = fract(P); // Fractional part for interpolation
  vec3 Pf1 = Pf0 - vec3(1.0); // Fractional part - 1.0
  vec4 ix = vec4(Pi0.x, Pi1.x, Pi0.x, Pi1.x);
  vec4 iy = vec4(Pi0.yy, Pi1.yy);
  vec4 iz0 = Pi0.zzzz;
  vec4 iz1 = Pi1.zzzz;

  vec4 ixy = permute(permute(ix) + iy);
  vec4 ixy0 = permute(ixy + iz0);
  vec4 ixy1 = permute(ixy + iz1);

  vec4 gx0 = ixy0 * (1.0 / 7.0);
  vec4 gy0 = fract(floor(gx0) * (1.0 / 7.0)) - 0.5;
  gx0 = fract(gx0);
  vec4 gz0 = vec4(0.5) - abs(gx0) - abs(gy0);
  vec4 sz0 = step(gz0, vec4(0.0));
  gx0 -= sz0 * (step(0.0, gx0) - 0.5);
  gy0 -= sz0 * (step(0.0, gy0) - 0.5);

  vec4 gx1 = ixy1 * (1.0 / 7.0);
  vec4 gy1 = fract(floor(gx1) * (1.0 / 7.0)) - 0.5;
  gx1 = fract(gx1);
  vec4 gz1 = vec4(0.5) - abs(gx1) - abs(gy1);
  vec4 sz1 = step(gz1, vec4(0.0));
  gx1 -= sz1 * (step(0.0, gx1) - 0.5);
  gy1 -= sz1 * (step(0.0, gy1) - 0.5);

  vec3 g000 = vec3(gx0.x,gy0.x,gz0.x);
  vec3 g100 = vec3(gx0.y,gy0.y,gz0.y);
  vec3 g010 = vec3(gx0.z,gy0.z,gz0.z);
  vec3 g110 = vec3(gx0.w,gy0.w,gz0.w);
  vec3 g001 = vec3(gx1.x,gy1.x,gz1.x);
  vec3 g101 = vec3(gx1.y,gy1.y,gz1.y);
  vec3 g011 = vec3(gx1.z,gy1.z,gz1.z);
  vec3 g111 = vec3(gx1.w,gy1.w,gz1.w);

  vec4 norm0 = taylorInvSqrt(vec4(dot(g000, g000), dot(g010, g010), dot(g100, g100), dot(g110, g110)));
  g000 *= norm0.x;
  g010 *= norm0.y;
  g100 *= norm0.z;
  g110 *= norm0.w;
  vec4 norm1 = taylorInvSqrt(vec4(dot(g001, g001), dot(g011, g011), dot(g101, g101), dot(g111, g111)));
  g001 *= norm1.x;
  g011 *= norm1.y;
  g101 *= norm1.z;
  g111 *= norm1.w;

  float n000 = dot(g000, Pf0);
  float n100 = dot(g100, vec3(Pf1.x, Pf0.yz));
  float n010 = dot(g010, vec3(Pf0.x, Pf1.y, Pf0.z));
  float n110 = dot(g110, vec3(Pf1.xy, Pf0.z));
  float n001 = dot(g001, vec3(Pf0.xy, Pf1.z));
  float n101 = dot(g101, vec3(Pf1.x, Pf0.y, Pf1.z));
  float n011 = dot(g011, vec3(Pf0.x, Pf1.yz));
  float n111 = dot(g111, Pf1);

  vec3 fade_xyz = fade(Pf0);
  vec4 n_z = mix(vec4(n000, n100, n010, n110), vec4(n001, n101, n011, n111), fade_xyz.z);
  vec2 n_yz = mix(n_z.xy, n_z.zw, fade_xyz.y);
  float n_xyz = mix(n_yz.x, n_yz.y, fade_xyz.x); 
  return 2.2 * n_xyz;
}

// Classic Perlin noise, periodic variant
float pnoise(vec3 P, vec3 rep)
{
  vec3 Pi0 = mod(floor(P), rep); // Integer part, modulo period
  vec3 Pi1 = mod(Pi0 + vec3(1.0), rep); // Integer part + 1, mod period
  Pi0 = mod289(Pi0);
  Pi1 = mod289(Pi1);
  vec3 Pf0 = fract(P); // Fractional part for interpolation
  vec3 Pf1 = Pf0 - vec3(1.0); // Fractional part - 1.0
  vec4 ix = vec4(Pi0.x, Pi1.x, Pi0.x, Pi1.x);
  vec4 iy = vec4(Pi0.yy, Pi1.yy);
  vec4 iz0 = Pi0.zzzz;
  vec4 iz1 = Pi1.zzzz;

  vec4 ixy = permute(permute(ix) + iy);
  vec4 ixy0 = permute(ixy + iz0);
  vec4 ixy1 = permute(ixy + iz1);

  vec4 gx0 = ixy0 * (1.0 / 7.0);
  vec4 gy0 = fract(floor(gx0) * (1.0 / 7.0)) - 0.5;
  gx0 = fract(gx0);
  vec4 gz0 = vec4(0.5) - abs(gx0) - abs(gy0);
  vec4 sz0 = step(gz0, vec4(0.0));
  gx0 -= sz0 * (step(0.0, gx0) - 0.5);
  gy0 -= sz0 * (step(0.0, gy0) - 0.5);

  vec4 gx1 = ixy1 * (1.0 / 7.0);
  vec4 gy1 = fract(floor(gx1) * (1.0 / 7.0)) - 0.5;
  gx1 = fract(gx1);
  vec4 gz1 = vec4(0.5) - abs(gx1) - abs(gy1);
  vec4 sz1 = step(gz1, vec4(0.0));
  gx1 -= sz1 * (step(0.0, gx1) - 0.5);
  gy1 -= sz1 * (step(0.0, gy1) - 0.5);

  vec3 g000 = vec3(gx0.x,gy0.x,gz0.x);
  vec3 g100 = vec3(gx0.y,gy0.y,gz0.y);
  vec3 g010 = vec3(gx0.z,gy0.z,gz0.z);
  vec3 g110 = vec3(gx0.w,gy0.w,gz0.w);
  vec3 g001 = vec3(gx1.x,gy1.x,gz1.x);
  vec3 g101 = vec3(gx1.y,gy1.y,gz1.y);
  vec3 g011 = vec3(gx1.z,gy1.z,gz1.z);
  vec3 g111 = vec3(gx1.w,gy1.w,gz1.w);

  vec4 norm0 = taylorInvSqrt(vec4(dot(g000, g000), dot(g010, g010), dot(g100, g100), dot(g110, g110)));
  g000 *= norm0.x;
  g010 *= norm0.y;
  g100 *= norm0.z;
  g110 *= norm0.w;
  vec4 norm1 = taylorInvSqrt(vec4(dot(g001, g001), dot(g011, g011), dot(g101, g101), dot(g111, g111)));
  g001 *= norm1.x;
  g011 *= norm1.y;
  g101 *= norm1.z;
  g111 *= norm1.w;

  float n000 = dot(g000, Pf0);
  float n100 = dot(g100, vec3(Pf1.x, Pf0.yz));
  float n010 = dot(g010, vec3(Pf0.x, Pf1.y, Pf0.z));
  float n110 = dot(g110, vec3(Pf1.xy, Pf0.z));
  float n001 = dot(g001, vec3(Pf0.xy, Pf1.z));
  float n101 = dot(g101, vec3(Pf1.x, Pf0.y, Pf1.z));
  float n011 = dot(g011, vec3(Pf0.x, Pf1.yz));
  float n111 = dot(g111, Pf1);

  vec3 fade_xyz = fade(Pf0);
  vec4 n_z = mix(vec4(n000, n100, n010, n110), vec4(n001, n101, n011, n111), fade_xyz.z);
  vec2 n_yz = mix(n_z.xy, n_z.zw, fade_xyz.y);
  float n_xyz = mix(n_yz.x, n_yz.y, fade_xyz.x); 
  return 2.2 * n_xyz;
}

    varying vec2 vUv;
    varying vec3 vReflect;
    varying vec3 pos;
    varying float ao;
    uniform float time;
    uniform float weight;
    uniform float noise_amt;
    varying float d;
    //attribute vec4 pcolor;
    //varying vec4 mcolor;

    float stripes( float x, float f) {
        float PI = 3.14159265358979323846264;
        float t = .5 + .5 * sin( f * 2.0 * PI * x);
        return t * t - .5;
    }
    
    float turbulence( vec3 p ) {
        float w = 100.0;
        float t = -.5;
        for (float f = 1.0 ; f <= 10.0 ; f++ ){
            float power = pow( 2.0, f );
            t += abs( pnoise( vec3( power * p ), vec3( 10.0, 10.0, 10.0 ) ) / power );
        }
        return t;
    }

    void main() {
        //mcolor = pcolor;
        vUv = uv;
    
        vec4 mPosition = modelMatrix * vec4( position, 1.0 );
        vec3 nWorld = normalize( mat3( modelMatrix[0].xyz, modelMatrix[1].xyz, modelMatrix[2].xyz ) * normal );
        vReflect = normalize( reflect( normalize( mPosition.xyz - cameraPosition ), nWorld ) );
        
        pos = position;
        //float noise = .3 * pnoise( 8.0 * vec3( normal ) );
        float noise = noise_amt *  -.10 * turbulence( .5 * normal + time );
        //float noise = - stripes( normal.x + 2.0 * turbulence( normal ), 1.6 );

        float displacement = - weight * noise;
        displacement += 1.0 * pnoise( 0.05 * position + vec3( 2.0 * time ), vec3( 10.0 ) );
        
        ao = noise;
        vec3 newPosition = position + normal * vec3( displacement );
        gl_Position = projectionMatrix * modelViewMatrix * vec4( newPosition, 1.0 );
    
    }

    </script>
    
    <script type="x-shader/x-vertex" id="perlin_fragmentShader">

    varying vec2 vUv;
    uniform sampler2D tExplosion;
    uniform vec3 pcolor;
    varying vec3 vReflect;
    varying vec3 pos;
    varying float ao;
    varying float d;
    float PI = 3.14159265358979323846264;

    float random(vec3 scale,float seed){return fract(sin(dot(gl_FragCoord.xyz+seed,scale))*43758.5453+seed);}

    void main() {

        vec3 color = texture2D( tExplosion, vec2( 0, 1.0 - 1.3 * ao + .001 * random(vec3(12.9898,78.233,151.7182),0.0) ) ).rgb;
        gl_FragColor = vec4( color.rgb, 1 ) * vec4(pcolor,1);

    }
    
    </script>
    <script type="x-shader/x-vertex" id="fs_ZoomBlur">
    
    varying vec2 vUv;
    uniform sampler2D tDiffuse;
    uniform vec2 resolution;
    uniform float strength;
    uniform float blurPct;
    
    float random(vec3 scale,float seed){return fract(sin(dot(gl_FragCoord.xyz+seed,scale))*43758.5453+seed);}
    
    void main() {

        vec2 center = .5 * resolution;
        
        vec4 color=vec4(0.0);
        float total=0.0;
        float bpc=blurPct*100.13;
        vec2 toCenter=center-vUv*resolution;
        float offset=random(vec3(12.9898,78.233,151.7182),0.0);
        for(float t=0.0;t<=40.0;t++){
            float percent=(t+offset)/bpc;
            float weight=4.0*(percent-percent*percent);
            vec4 sample=texture2D(tDiffuse,vUv+toCenter*percent*strength/resolution);
            sample.rgb*=sample.a;
            color+=sample*weight;
            total+=weight;
        }
        
        gl_FragColor=color/total;
        //gl_FragColor.a = blurPct;
        gl_FragColor.rgb/=gl_FragColor.a+0.001;
        
    }
    
    </script>
    <script type="x-shader/x-vertex" id="fs_Composite">

    varying vec2 vUv;
    uniform sampler2D tBase;
    uniform sampler2D tGlow;

    void main() {

        
        //Screen: X = 1- ((255-U)*(255-L))/255
        vec4 color = 1.0 - ( ( 1.0 - texture2D( tGlow, vec2( vUv.x, 1.0 - vUv.y ) ) ) * ( 1.0 - texture2D( tBase, vUv ) ) );
        //vec4 color = mix( texture2D( tBase, vUv ), texture2D( tGlow, vec2( vUv.x, 1.0 - vUv.y ) ), .5 );
        //vec4 color = texture2D( tBase, vUv ) + texture2D( tGlow, vUv ) * texture2D( tGlow, vUv );
        //vec4 color = texture2D( tGlow, vUv );
        
        gl_FragColor = vec4( color.rgb, 1.0 );

    }
    
    </script>
    <script type="x-shader/x-vertex" id="blur_vertexShader">
            
        varying vec2 vUv;

        void main() {

            vUv = uv;
            gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );

        }
        
    </script>
<!-- end fancypants -->
<script src="js/ocuLeap.js"></script>
<script src="js/local.js"></script>
</head>
<body>
<div id="debug"></div>
<div id="instruction">
  Use leap to fly. The bigger you open your hand, the faster you go / vice versa. Draw a circle to rotate the camera. Press 'r' to reset your relative home point.<br />
  If you have an Oculus Rift (and the <a href="https://github.com/benvanik/vr.js/tree/master">vr.js plugin at github</a>), plug it in...<br />
  I recommend using headphones to get the most out of the 3d HTML5 audio experience...
</div>
</body>
</html>